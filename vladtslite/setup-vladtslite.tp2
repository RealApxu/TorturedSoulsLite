///////////////////////////////
// Tortured Souls Lite
///////////////////////////////

BACKUP ~weidu_external/backup/vladtslite~
AUTHOR ~Vladislav Papper (vladp6@gmail.com)~
VERSION ~0.4.1~

NO_IF_EVAL_BUG
AUTO_EVAL_STRINGS

ALWAYS
 ACTION_IF (GAME_IS ~eet~) BEGIN
 OUTER_SET bg2_chapter = 12
 END ELSE BEGIN
 OUTER_SET bg2_chapter = 0
 END
 OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
 OUTER_SET bg2_chapter = bg2_chapter + 1
 OUTER_SPRINT name_source ~bg2_chapter_%i%~
 OUTER_SET ~%name_source%~ = bg2_chapter
 END
END

AUTO_TRA ~%MOD_FOLDER%/tra/%s~

LANGUAGE ~English~
~english~
~%MOD_FOLDER%/language/english/setup.tra~

/*
LANGUAGE ~Russian~
~russian~
~%MOD_FOLDER%/tra/russian/setup.tra~
*/

///////////////////////////////
// Main Component
///////////////////////////////

BEGIN ~Vlad Compilation - Tortured Souls Lite~
DESIGNATED 0
LABEL ~vlad_tslite_main~
REQUIRE_PREDICATE (GAME_IS ~BG2EE EET~) ~Tortured Souls Lite can only be installed on BG2:EE or EET.~
INCLUDE ~%MOD_FOLDER%/main.tpa~
LAF tslite_main END

///////////////////////////////
// Throne of Bhaal component
///////////////////////////////

BEGIN ~Tortured Souls Lite - ToB Expansion~
DESIGNATED 10
LABEL ~vlad_tslite_tob~
REQUIRE_PREDICATE (GAME_IS ~BG2EE EET~) ~Tortured Souls Lite can only be installed on BG2:EE or EET.~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~
INCLUDE ~%MOD_FOLDER%/tob.tpa~
LAF tslite_tob END

///////////////////////////////
// Portraits
///////////////////////////////

BEGIN ~Tortured Souls Lite - Kachiko Original Portrait (poemmaker2)~
DESIGNATED 20
LABEL ~vlad_tslite_kachikoportrait_original~
SUBCOMPONENT ~Kachiko Portrait~
REQUIRE_PREDICATE (GAME_IS ~BG2EE EET~) ~Tortured Souls Lite can only be installed on BG2:EE or EET.~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY ~%MOD_FOLDER%/portraits/ttkachi1l.bmp~ ~override/ttkachiL.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi1m.bmp~ ~override/ttkachiM.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi1s.bmp~ ~override/ttkachiS.bmp~

BEGIN ~Tortured Souls Lite - Kachiko Alternate Portrait 1 (Enkida)~
DESIGNATED 21
LABEL ~vlad_tslite_kachikoportrait_alt1~
SUBCOMPONENT ~Kachiko Portrait~
REQUIRE_PREDICATE (GAME_IS ~BG2EE EET~) ~Tortured Souls Lite can only be installed on BG2:EE or EET.~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY ~%MOD_FOLDER%/portraits/ttkachi2l.bmp~ ~override/ttkachiL.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi2m.bmp~ ~override/ttkachiM.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi2s.bmp~ ~override/ttkachiS.bmp~

BEGIN ~Tortured Souls Lite - Kachiko Alternate Portrait 2~
DESIGNATED 22
LABEL ~vlad_tslite_kachikoportrait_alt2~
SUBCOMPONENT ~Kachiko Portrait~
REQUIRE_PREDICATE (GAME_IS ~BG2EE EET~) ~Tortured Souls Lite can only be installed on BG2:EE or EET.~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY ~%MOD_FOLDER%/portraits/ttkachi3l.bmp~ ~override/ttkachiL.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi3m.bmp~ ~override/ttkachiM.bmp~
COPY ~%MOD_FOLDER%/portraits/ttkachi3s.bmp~ ~override/ttkachiS.bmp~

///////////////////////////////
// Soundsets
///////////////////////////////

BEGIN ~Tortured Souls Lite - Kachiko Default Soundset~
DESIGNATED 30
LABEL ~vlad_tslite_kachikosoundset_new~
SUBCOMPONENT ~Kachiko Soundset~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY_EXISTING ~ttkachi.cre~ override
 SAY MORALE ~Uhm, I have to go now...~ [KACHIMO]
 SAY LEADER ~I will try to fill this role as well as I can.~ [KACHIMC]
 SAY BATTLE_CRY1 ~My aim shall be true!~ [KACHIBC1]
 SAY BATTLE_CRY2 ~Onward, to glorious battle!~ [KACHIBC2]
 SAY BATTLE_CRY3 ~Into the fray!~ [KACHIBC3]
 SAY BATTLE_CRY4 ~Time to test my metal!~ [KACHIBC4]
 SAY BATTLE_CRY5 ~Taking it to them!~ [KACHIBC5]
 SAY TIRED ~Let me find some place to set camp.~ [KACHITI]
 SAY BORED ~Am I to stay here all day long?~ [KACHIBO]
 SAY DAMAGE ~[Kachiko Damaged Sound]~ [KACHIST]
 SAY DYING ~[Kachiko Dying Sound]~ [KACHIDI]
 SAY HURT ~Help me... I am in peril!~ [KACHIWO]
 SAY AREA_DUNGEON ~[Kachiko In Dungeon Sound]~ [KACHIDU]
 SAY AREA_FOREST ~[Kachiko In Forest Sound]~ [KACHIOU]
 SAY AREA_DAY ~[Kachiko Day Time Sound]~ [KACHIDA]
 SAY AREA_NIGHT ~[Kachiko Night Time Sound]~ [KACHINI]
 SAY SELECT_COMMON1 ~I am prepared... I hope.~ [KACHISE1]
 SAY SELECT_COMMON2 ~Yes?~ [KACHISE2]
 SAY SELECT_COMMON3 ~Let me know what you want.~ [KACHISE3]
 SAY SELECT_COMMON4 ~I await your orders.~ [KACHISE4]
 SAY SELECT_COMMON5 ~Go ahead.~ [KACHISE5]
 SAY SELECT_COMMON6 ~Your command?~ [KACHIAC6]
 SAY SELECT_ACTION1 ~I'm on it.~ [KACHIAC1]
 SAY SELECT_ACTION2 ~Understood.~ [KACHIAC2]
 SAY SELECT_ACTION3 ~That sounds reasonable.~ [KACHIAC3]
 SAY SELECT_ACTION4 ~Yes.~ [KACHIAC4]
 SAY SELECT_ACTION5 ~Agreed.~ [KACHIAC5]
 SAY SELECT_ACTION6 ~My duty is clear.~ [KACHIAC6]
 SAY SELECT_ACTION7 ~Acknowledged.~ [KACHIAC7]
 SAY SELECT_RARE1 ~Yeah, yeah... Tell me what you want.~ [KACHIRA1]
 SAY SELECT_RARE1 ~What exactly are you trying to accomplish?~ [KACHIRA2]
 SAY REACT_TO_DIE_GENERAL ~No! You will not have died in vain!~ [KACHIRD]
 SAY CRITICAL_HIT ~[Kachiko Critical Hit Sound]~ [KACHICR]
 SAY CRITICAL_MISS ~[Kachiko Critical Miss Sound]~ [KACHIMI]
 SAY TARGET_IMMUNE ~[Kachiko Target Immunity Sound]~ [KACHIIM]
 SAY INVENTORY_FULL ~[Kachiko Inventory Full Sound]~ [KACHICA]
 SAY SPELL_DISRUPTED ~[Kachiko Spell Disrupted Sound]~ [KACHIFA]
 
LAF HANDLE_AUDIO
 STR_VAR
 audio_path = ~%MOD_FOLDER%/audio/ss_new~
 oggdec_path = ~%MOD_FOLDER%/audio~
 sox_path = ~%MOD_FOLDER%/audio~
END

BEGIN ~Tortured Souls Lite - Kachiko Legacy Soundset + Voiceover~
DESIGNATED 31
LABEL ~vlad_tslite_kachikosoundset_old~
SUBCOMPONENT ~Kachiko Soundset~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY_EXISTING ~ttkachi.cre~ override
 SAY LEADER ~Well, we're not out of the woods yet... Oh, wait, we are! *laughs*~ [KACHIMC]
 SAY BATTLE_CRY1 ~I'll make you sorry you attacked me... Right before you die!~ [KACHIBC]
 SAY BATTLE_CRY2 ~Don't you mess with me!~ [KACHIHA]
 SAY TIRED ~[Kachiko Tired Sound]~ [KACHITI]
 SAY 0xdc ~It's time for some action!~ [KACHIAT]
 SAY DAMAGE ~[Kachiko Damaged Sound]~ [KACHIST]
 SAY DYING ~[Kachiko Dying Sound]~ [KACHIDI]
 SAY HURT ~I won't survive any more wounds...~ [KACHIWO]
 SAY AREA_DUNGEON ~I hate closed spaces.~ [KACHIDU]
 SAY AREA_FOREST ~The air is so fresh.~ [KACHIOU]
 SAY AREA_DAY ~Oh, what a lovely day!~ [KACHIDA]
 SAY AREA_NIGHT ~Look at all the stars!~ [KACHINI]
 SAY SELECT_COMMON1 ~M-hm.~ [KACHISE1]
 SAY SELECT_COMMON2 ~What can I do?~ [KACHISE2]
 SAY SELECT_COMMON3 ~Yes?~ [KACHISE3]
 SAY SELECT_COMMON4 ~You wanted me?~ [KACHISE4]
 SAY SELECT_COMMON5 ~I'm here.~ [KACHISE5]
 SAY SELECT_COMMON6 ~That's an idea.~ [KACHIAC6]
 SAY SELECT_ACTION1 ~Sure.~ [KACHIAC1]
 SAY SELECT_ACTION2 ~Sounds good.~ [KACHIAC2]
 SAY SELECT_ACTION3 ~Okay.~ [KACHIAC3]
 SAY SELECT_ACTION4 ~I'd love to!~ [KACHIAC4]
 SAY SELECT_ACTION5 ~Oh, I got it!~ [KACHIAC5]
 SAY SELECT_ACTION6 ~That's an idea.~ [KACHIAC6]
 SAY CRITICAL_HIT ~Wow, good strike!~ [KACHICR]
 SAY CRITICAL_MISS ~Couldn't hurt.~ [KACHIIM]
 SAY TARGET_IMMUNE ~I can't hurt it!~ [KACHIBA]
 SAY INVENTORY_FULL ~Oops! I'm carrying too much.~ [KACHICA]
 SAY SPELL_DISRUPTED ~Oh, fiddlesticks!~ [KACHIFA]
 
LAF HANDLE_AUDIO
 STR_VAR
 audio_path = ~%MOD_FOLDER%/audio/ss_old~
 oggdec_path = ~%MOD_FOLDER%/audio~
 sox_path = ~%MOD_FOLDER%/audio~
END

LAF HANDLE_AUDIO
 STR_VAR
 audio_path = ~%MOD_FOLDER%/audio/vo_old~
 oggdec_path = ~%MOD_FOLDER%/audio~
 sox_path = ~%MOD_FOLDER%/audio~
END

///////////////////////////////
//NPC Portraits
///////////////////////////////

BEGIN ~Tortured Souls Lite - Portraits for Minor Mod NPCs~
DESIGNATED 50
LABEL ~vlad_tslite_miscportraits~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~Setup-TorturedSoulsLite.tp2~ ~0~) ~Please, install Tortured Souls Main Component~

COPY ~%MOD_FOLDER%/portraits/npc~ override