///////////////////////
// Experience Set
///////////////////////

IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,1249999)
	XPLT(Myself,1250000)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,1250000,SET)
	  SetInterrupt(TRUE)
END
  
IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,999999)
	XPLT(Myself,1000000)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,1000000,SET)
	  SetInterrupt(TRUE)
END
  
  IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,749999)
	XPLT(Myself,750000)
  THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,750000,SET)
	  SetInterrupt(TRUE)
END
  
IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,499999)
	XPLT(Myself,500000)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,500000,SET)
	  SetInterrupt(TRUE)
END
  
IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,249999)
	XPLT(Myself,250000)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,250000,SET)
	  SetInterrupt(TRUE)
END
  
IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPGT(Player1,124999)
	XPLT(Myself,125000)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  ChangeStat(Myself,XP,125000,SET)
	  SetInterrupt(TRUE)
END
  
IF
	Global("ttKachiExpSet","GLOBAL",0)
	InParty(Myself)
	XPLT(Player1,89001)
THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
	  SetGlobal("ttKachiExpSet","GLOBAL",1)
	  SetInterrupt(TRUE)
END

////////////////////////
// First meeting
////////////////////////

IF
	See([PC])
	See("Yoshimo")
	InParty("Yoshimo")
    InMyArea("Yoshimo")
	!Dead("Yoshimo")
	!StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck("Yoshimo",CD_STATE_NOTVALID)
	See("Yoshimo")
	Global("ttKachiko_Meet","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("ttKachiko_Meet","GLOBAL",2)
		StartDialogNoSet("Yoshimo")
END

///////////////////////////////
// Kachiko-Yoshimo Romance
///////////////////////////////

// Starting Romance
IF
	InParty(Myself)
	InParty("Yoshimo")
	!Dead("Yoshimo")
	GlobalLT("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("ttOnIsland","GLOBAL",1)
	Global("ttKachikoRomance","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobalTimer("ttKachikoRomanceTimer","GLOBAL",ONE_DAY)
		SetGlobal("ttKachikoRomance","GLOBAL",1)
END

// Lovetalks 1-2 are in the Dream Script

// Talks 3-4
IF
	InParty(Myself)
	InParty("Yoshimo")
	See("Yoshimo")
	!Dead("Yoshimo")
	!StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck("Yoshimo",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	GlobalTimerExpired("ttKachikoRomanceTimer","GLOBAL")
	GlobalLT("Chapter","GLOBAL",%bg2_chapter_4%)
	!Global("ttOnIsland","GLOBAL",1)
	OR(2)
		Global("ttKachikoRomance","GLOBAL",3)
		Global("ttKachikoRomance","GLOBAL",5)
THEN
	RESPONSE #100
		IncrementGlobal("ttKachikoRomance","GLOBAL",1)
		PlaySong(0)
		PlaySound("ttkachls")
		Interact("Yoshimo")
END

// Lovetalk 5 is in the Dream Script

// Talk 6

IF
	InParty(Myself)
	Global("ttYoshimoExcuse","GLOBAL",4)
	Global("ttKachikoSaved","GLOBAL",2)
	See("Yoshimo")
	InParty("Yoshimo")
	!Dead("Yoshimo")
THEN
	RESPONSE #100
		SetGlobal("ttYoshimoExcuse","GLOBAL",5)
		SetGlobalTimer("ttKachiko_LastTalk","GLOBAL",ONE_DAY)
END

IF
	InParty(Myself)
	GlobalTimerExpired("ttKachiko_LastTalk","GLOBAL")
	Global("ttYoshimoExcuse","GLOBAL",5)
	InParty(Myself)
	See("Yoshimo")
	InParty("Yoshimo")
	!Dead("Yoshimo")
	!StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck("Yoshimo",CD_STATE_NOTVALID)
	CombatCounter(0)
	!Range([NEUTRAL],10)
	PartyRested()
THEN
	RESPONSE #100
		SetGlobal("ttYoshimoExcuse","GLOBAL",6)
		PlaySong(0)
		PlaySound("ttkachls")
		Interact("Yoshimo")
END

/////////////////////////////////
// Yoshimo is wounded or dead
/////////////////////////////////

IF
	InParty(Myself)
	InParty("Yoshimo")
	HPPercentLT("Yoshimo",20)
	!Dead("Yoshimo")
	!CombatCounter(0)
	Exists(LastAttackerOf("Yoshimo"))
	Allegiance(LastAttackerOf("Yoshimo"),ENEMY)
	Global("ttYoshimo_Dies","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("ttYoshimo_Dies","GLOBAL",1)
		DisplayStringHead(Myself,@247)
		Continue()
END

IF
	InParty(Myself)
	Dead("Yoshimo")
	CombatCounter(0)
	OR(2)
		Global("ttYoshimo_Dies","GLOBAL",0)
		Global("ttYoshimo_Dies","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("ttYoshimo_Dies","GLOBAL",2)
END

IF
	InParty(Myself)
	Dead("ttkachi")  // Kachiko
	!InParty("ttkachi")
	CombatCounter(0)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("ttYoshimo_Dies","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("ttYoshimo_Dies","GLOBAL",3)
		StartDialogueNoSet([PC])
END

IF
	InParty(Myself)
	InParty("Yoshimo")
	!Dead("Yoshimo")
	OR(2)
		Global("ttYoshimo_Dies","GLOBAL",1)
		Global("ttYoshimo_Dies","GLOBAL",2)
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("ttYoshimo_Dies","GLOBAL",0)
END

///////////////////////
// Fight Blocks
///////////////////////

IF
	Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("NPCFight","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("NPCFight","LOCALS",1)
		ChangeAIScript("",DEFAULT)
		ChangeAIScript("useitem",GENERAL)
		ChangeAIScript("ttkachix",SPECIFICS)
		SetDialog("")
		Continue()
END

IF
	Allegiance(Myself,NEUTRAL)
	AttackedBy([GOODCUTOFF],DEFAULT)
THEN
	RESPONSE #100
		ChangeAIScript("ttkachix",SPECIFICS)
		ChangeAIScript("useitem",GENERAL)
		ChangeAIScript("",DEFAULT)
		Enemy()
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	!InParty(Myself)
	Global("NPCFight","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("NPCFight","LOCALS",0)
		ChangeAIScript("",GENERAL)
		ChangeAIScript("",SPECIFICS)
		Continue()
END

IF
	Global("ttMyHomeAttacked","GLOBAL",1)
	Global("ttHashimotoAttacked","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("ttHashimotoAttacked","LOCALS",1)
		LeaveParty()
		Enemy()
END