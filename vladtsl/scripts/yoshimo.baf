/////////////////////////
// Spawning Kachiko
/////////////////////////

IF
  InParty(Myself)
  OR(6)
    AreaCheck("AR0020")
    AreaCheck("AR0300")
    AreaCheck("AR0500")
    AreaCheck("AR0700")
    AreaCheck("AR0900")
    AreaCheck("AR1000")
  GlobalLT("Chapter","GLOBAL",%bg_chapter_4%)
  Global("ttKachiko_Meet","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("ttKachiko_Meet","GLOBAL",1)
END

////////////////////////////////
// Kachiko is wounded or dead
////////////////////////////////

IF
  InParty(Myself)
  InParty("ttkachi")  // Kachiko
  HPPercentLT("ttkachi",20)  // Kachiko
  !Dead("ttkachi")  // Kachiko
  !CombatCounter(0)
  Exists(LastAttackerOf("Yoshimo"))
	Allegiance(LastAttackerOf("Yoshimo"),ENEMY)
  Global("ttKachikoDies","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("ttKachikoDies","GLOBAL",1)
    DisplayStringHead(Myself,@240)  // @240
    Continue()
END

IF
  InParty(Myself)
  Dead("ttkachi")  // Kachiko
  CombatCounter(0)
  OR(2)
    Global("ttKachikoDies","GLOBAL",0)
    Global("ttKachikoDies","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("ttKachikoDies","GLOBAL",2)
END

IF
  InParty(Myself)
  Dead("ttkachi")  // Kachiko
  !InParty("ttkachi")
  CombatCounter(0)
  See(Player1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  Global("ttKachikoDies","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("ttKachikoDies","GLOBAL",3)
    StartDialogueNoSet([PC])
END

IF
  InParty(Myself)
  InParty("ttkachi")  // Kachiko
  !Dead("ttkachi")  // Kachiko
  OR(2)
    Global("ttKachikoDies","GLOBAL",1)
    Global("ttKachikoDies","GLOBAL",2)
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("ttKachikoDies","GLOBAL",0)
END

//////////////////////////////
// Island Quest Interactions
//////////////////////////////

IF
  Global("ttMyHomeAttacked","GLOBAL",1)
  Global("ttHashimotoAttacked","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("ttHashimotoAttacked","LOCALS",1)
    LeaveParty()
    Enemy()
END

IF
  Global("ttKachikoKidnapped","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogNoSet([PC])
END